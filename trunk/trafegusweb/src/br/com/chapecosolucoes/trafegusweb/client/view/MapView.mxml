<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 initialize="init()"
		 xmlns:maps="com.google.maps.*">
	<s:layout>
		<s:BasicLayout/>
	</s:layout>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Script>
		<![CDATA[
			import br.com.chapecosolucoes.trafegusweb.client.controller.MapController;
			import br.com.chapecosolucoes.trafegusweb.client.model.MainModel;
			import br.com.chapecosolucoes.trafegusweb.client.vo.HistoricoPosicoesVeiculoVO;
			import br.com.chapecosolucoes.trafegusweb.client.vo.PosicaoVeiculoVO;
			
			import com.google.maps.LatLng;
			import com.google.maps.MapMouseEvent;
			import com.google.maps.examples.TooltipOverlay;
			import com.google.maps.overlays.Marker;
			import com.google.maps.overlays.MarkerOptions;
			import com.google.maps.overlays.Polyline;
			import com.google.maps.overlays.PolylineOptions;
			import com.google.maps.styles.StrokeStyle;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			private var controller:MapController;
			private function init():void
			{
				controller = new MapController();
				controller.view = this;
			}
			public function rotaVeiculo(value:ArrayCollection):void
			{
				var historicoPosicoes:Array = new Array();
				for each (var route:HistoricoPosicoesVeiculoVO in value)
				{
					historicoPosicoes.push(route.latLong);
				}
				var polyLine:Polyline = new Polyline
				(
					historicoPosicoes,
					new PolylineOptions
					({
						strokeStyle: new StrokeStyle(
							{
								color: 0x0000ff,
								thickness: 4,
								alpha: 0.7
							})
					})
				);
				map.addOverlay(polyLine);
			}
			public function posicaoVeiculosArray(value:ArrayCollection):void
			{
				if(MainModel.getInstance().mapReady == true)
				{
					map.clearOverlays();
					for each (var posVeiculoVO:PosicaoVeiculoVO in value)
					{
						map.addOverlay(createMarker(posVeiculoVO));
					}
					map.setCenter(new LatLng(-15.779413016690475, -47.929744720458984), 4);
				}
			}
			public function posicaoVeiculoVO(value:PosicaoVeiculoVO):void
			{
				//MainModel.getInstance().posicaoVeiculoVO = value;
				map.setCenter(MainModel.getInstance().posicaoVeiculoVO.latLong, 15);
			}
			[Embed(source="assets/caminhao_verde.png")]
			private var IconeCaminhaoVerde:Class;
			
			[Embed(source="assets/caminhao_amarelo.png")]
			private var IconeCaminhaoAmarelo:Class;
			public function createMarker(posicaoVeiculoVO:PosicaoVeiculoVO):Marker
			{
				var makerOp:MarkerOptions = new MarkerOptions();
				if (posicaoVeiculoVO.ignition == 0)
					makerOp.icon = new IconeCaminhaoAmarelo();
				else
					makerOp.icon = new IconeCaminhaoVerde();
				
				var marker:Marker = new Marker(posicaoVeiculoVO.latLong, makerOp);
				var tooltip:TooltipOverlay = new TooltipOverlay(posicaoVeiculoVO.latLong, posicaoVeiculoVO.gpsDescSis);
				marker.addEventListener(MapMouseEvent.CLICK, this.controller.mapMouseEventHandler);
				marker.addEventListener(MapMouseEvent.ROLL_OVER, function(e:MapMouseEvent):void
				{
					map.addOverlay(tooltip);
				});
				marker.addEventListener(MapMouseEvent.ROLL_OUT, function(e:MapMouseEvent):void
				{
					map.removeOverlay(tooltip);
				});
				return marker;
			}
		]]>
	</fx:Script>
	<maps:Map id="map"
			  key="ABQIAAAAzLOtdcPXfM2lE08HYYoOtBRjLyj5ojzoC0E4DRI1gCu_z1OhKRTdmypwjwm7FVWCeee1VpIL-w0z1A"
			  mapevent_mapready="controller.onMapReady(event)"
			  width="100%"
			  height="100%"/>
</s:Group>
